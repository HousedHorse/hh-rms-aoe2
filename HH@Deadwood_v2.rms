/* some const definitions */
#const DLC_FALCON   1056
#const DEADTREE     404
#const DEADTREE2    407
#const GRASS_CLIFFS 16

<PLAYER_SETUP>          /* basic setup */
random_placement

<LAND_GENERATION>       /* main lands and seas */
base_terrain LEAVES

create_player_lands {
  terrain_type DLC_CRACKED
  land_percent 100
  other_zone_avoidance_distance 10
  base_size 10
  set_zone_by_team
  border_fuzziness 100
  clumping_factor 100
  right_border  -2
  bottom_border -2
  left_border   -2
  top_border    -2
}

<ELEVATION_GENERATION>  /* hills */
create_elevation 4 {
  base_terrain DLC_CRACKED /* change this to match base_terrain of map */
  number_of_clumps 3
  number_of_tiles 300
  set_scale_by_groups
  set_scale_by_size
  spacing 3
}

<TERRAIN_GENERATION>    /* terrain patches */
/* forest */
create_terrain LEAVES {
  base_terrain DLC_CRACKED
  land_percent 13
  number_of_clumps 12
  spacing_to_other_terrain_types 5
  set_avoid_player_start_areas
  set_scale_by_groups
}

/* oases */
create_terrain DESERT {
	base_terrain DLC_CRACKED
  land_percent 8
  number_of_clumps 4
  set_scale_by_groups
  spacing_to_other_terrain_types 0
  set_avoid_player_start_areas
  set_flat_terrain_only
}
create_terrain DLC_WATER5 {
	base_terrain DESERT
  land_percent 100
  number_of_clumps 99999
  spacing_to_other_terrain_types 2
  set_avoid_player_start_areas
  set_flat_terrain_only
}

<CONNECTION_GENERATION> /* bridges/roads */
create_connect_all_players_land {
  replace_terrain LEAVES DLC_CRACKED
  replace_terrain DLC_CRACKED DLC_CRACKED
  terrain_cost DLC_CRACKED 2
  terrain_cost LEAVES 10
  terrain_size LEAVES  4 0  
}

<OBJECTS_GENERATION>    /* units, buildings, resources, eyecandy */

/* normal start */
create_object TOWN_CENTER {
  set_place_for_every_player
  max_distance_to_players 0
  min_distance_to_players 0
}
create_object SCOUT {
  set_place_for_every_player
  min_distance_to_players 4
  max_distance_to_players 6
}
create_object VILLAGER {
  set_place_for_every_player
  min_distance_to_players 4
  max_distance_to_players 6
}

/* relics */
if LUDIKRIS_MAP
  create_object RELIC {
    number_of_objects            250
    min_distance_to_players     25
    temp_min_distance_group_placement 110
    max_distance_to_other_zones 4
  }
else
  create_object RELIC {
    number_of_objects	1
    number_of_groups 2
    set_gaia_object_only
    set_place_for_every_player
    min_distance_to_players 20
    max_distance_to_players 26
    temp_min_distance_group_placement 20
    max_distance_to_other_zones 4
    terrain_to_place_on DLC_CRACKED
  }
endif

/* if regicide... */
if REGICIDE
  create_object KING {
    set_place_for_every_player
    min_distance_to_players 4
    max_distance_to_players 6
  }
  create_object CASTLE {
    set_place_for_every_player
    min_distance_to_players 10
    max_distance_to_players 20
  }
  create_object VILLAGER {
    set_place_for_every_player
    min_distance_to_players 4
    max_distance_to_players 6
    number_of_objects 7
  }
endif

/* create "dead forests" */
create_object DEADTREE2 {
  terrain_to_place_on LEAVES
  number_of_objects 999999
  temp_min_distance_group_placement 1
}
create_object DEADTREE {
  terrain_to_place_on LEAVES
  number_of_objects 999999
}

/* palm trees for the oasis */
create_object PALMTREE {
  number_of_groups 99999
  number_of_objects 2
  group_variance 1
  terrain_to_place_on DESERT
  temp_min_distance_group_placement 3
}

/* berries */
/* FORAGE / DLC_ORANGEBUSH */
create_object FORAGE {
  number_of_objects 6
  group_placement_radius 3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 10
  max_distance_to_players 12
  min_distance_group_placement 6
}

/* near gold */
create_object GOLD {
  number_of_objects 7
  group_placement_radius 3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 12
  max_distance_to_players 16
  min_distance_group_placement 5
  max_distance_to_other_zones 7
}

/* medium gold */
create_object GOLD {
  number_of_objects 4
  group_placement_radius 3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 18
  max_distance_to_players 26
  min_distance_group_placement 5
  max_distance_to_other_zones 7
}

/* far gold */
create_object GOLD {
  number_of_objects 4
  group_placement_radius 3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 22
  max_distance_to_players 26
  min_distance_group_placement 5
  max_distance_to_other_zones 7
}

/* near stone */
create_object STONE {
  number_of_objects 5
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 14
  max_distance_to_players 18
  min_distance_group_placement 5
  max_distance_to_other_zones 7
}

/* far stone */
create_object STONE {
  number_of_objects 4
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 20
  max_distance_to_players 26
  min_distance_group_placement 5
  max_distance_to_other_zones 7
}

/* sheepies */
/* SHEEP / TURKEY / DLC_GOAT
   DLC_LLAMA / DLC_WATERBUFFALO
   DLC_COW */
create_object DLC_COW {
  number_of_objects 4
  number_of_groups 2
  temp_min_distance_group_placement 2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 6
  max_distance_to_players 6
  min_distance_group_placement 1
  temp_min_distance_group_placement 4
}

/* deer patch */
/* DEER / DLC_OSTRICH / DLC_ZEBRA */
create_object DLC_OSTRICH {
  number_of_groups 1
  number_of_objects 4
  group_placement_radius 3
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 12
  max_distance_to_players 16
  min_distance_group_placement 3
}

/* boars */
/* BOAR / JAVELINA / DLC_ELEPHANT
   DLC_RHINO */
create_object BOAR {
  number_of_objects 1
  number_of_groups 3
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 12
  max_distance_to_players 16
  min_distance_group_placement 3
}

/* stragglers */
/* OAKTREE / PINETREE / SNOWPINETREE
   PALMTREE / JUNGLETREE / DLC_RAINTREE
   DLC_DRAGONTREE / DLC_BAOBABTREE 
   DEADTREE / DEADTREE2 / DLC_MANGROVE_TREE 
   BAMBOO_TREE */
create_object DEADTREE {
  number_of_objects 2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 4
  max_distance_to_players 5
  min_distance_group_placement 2
}
create_object DEADTREE2 {
  number_of_objects 3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 6
  max_distance_to_players 8
  min_distance_group_placement 2
}

/* extra ostriches */
create_object DLC_OSTRICH {
  number_of_objects 5
  number_of_groups 3
  group_placement_radius 4
  group_variance 1
  set_gaia_object_only
  set_place_for_every_player
  temp_min_distance_group_placement 40
  min_distance_to_players 35
  max_distance_to_players 50
}

/* predatory lifeforms */
/* WOLF / JAGUAR / DLC_KOMODO / DLC_TIGER 
   DLC_LION / DLC_CROCODILE / DLC_BEAR */
create_object WOLF {
  number_of_objects 2
  set_gaia_object_only
  set_place_for_every_player
  temp_min_distance_group_placement 30
  min_distance_to_players 35
  max_distance_to_players 40
}

/* extra gold */
create_object GOLD {
  if TINY_MAP
    number_of_groups 2
    number_of_objects 3
    min_distance_group_placement 5
  elseif SMALL_MAP
    number_of_groups 2
    number_of_objects 3
    min_distance_group_placement 5
  elseif MEDIUM_MAP
    number_of_groups 3
    number_of_objects 3
    group_variance 1
    min_distance_group_placement 5
  elseif LARGE_MAP
    number_of_groups 3
    number_of_objects 3
    group_variance 1
    min_distance_group_placement 5
  elseif HUGE_MAP
    number_of_groups 4
    number_of_objects 4
    group_variance 1
    min_distance_group_placement 5
  elseif GIGANTIC_MAP
    number_of_groups 5
    number_of_objects 4
    group_variance 1
    min_distance_group_placement 5
  elseif LUDIKRIS_MAP
    number_of_groups 250
    number_of_objects 4
    group_variance 1
    temp_min_distance_group_placement  44
  endif 
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players 35
}

/* extra stone */
create_object STONE {
  if TINY_MAP
    number_of_groups 1
    min_distance_group_placement 5
  elseif SMALL_MAP
    number_of_groups 1
    min_distance_group_placement 5
  elseif MEDIUM_MAP
    number_of_groups 2
    min_distance_group_placement 5
  elseif LARGE_MAP
    number_of_groups 3
    min_distance_group_placement 5
  elseif HUGE_MAP
    number_of_groups 4
    min_distance_group_placement 5
  elseif GIGANTIC_MAP
    number_of_groups 5
    min_distance_group_placement 5
  elseif LUDIKRIS_MAP
    number_of_groups 250
    temp_min_distance_group_placement 44
  endif 
  number_of_objects 4
  group_variance 1
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players 35
}

/* birds */
/* HAWK / MACAW / DLC_STORK / DLC_VULTURE / DLC_FALCON */
create_object DLC_VULTURE {
  number_of_objects 6
  set_scaling_to_map_size
}

/* fishies */
create_object SHORE_FISH {
  number_of_objects 9999
  temp_min_distance_group_placement 3
}

/* eye candy */
create_object DLC_ANIMALSKELETON {
  min_distance_to_players 10
  number_of_objects 10
  set_scaling_to_map_size
  temp_min_distance_group_placement 20
  terrain_to_place_on DLC_CRACKED
}
create_object SKELETON {
  min_distance_to_players 10
  number_of_objects 5
  set_scaling_to_map_size
  temp_min_distance_group_placement 40
  terrain_to_place_on DLC_CRACKED
}
create_object CACTUS {
  number_of_objects 20
  set_scaling_to_map_size
  temp_min_distance_group_placement 60
  terrain_to_place_on DLC_CRACKED
}
