
/* **************************************** */
/* Map:     '                               */
/* Version: '                               */
/* Author:  HousedHorse                     */
/* Date:    '                               */
/* **************************************** */

/* some const definitions */
#const DLC_FALCON   1056
#const DEADTREE     404
#const DEADTREE2    407
#const GRASS_CLIFFS 16

<PLAYER_SETUP>          /* basic setup */
random_placement

<LAND_GENERATION>       /* main lands and seas */
base_terrain SNOW

create_land {
  terrain_type SNOW_FOREST
  land_percent 100
  border_fuzziness 100
  clumping_factor 100
}

create_player_lands {
  terrain_type SNOW
  land_percent 100
  base_size 10
  left_border   22
  right_border  22
  top_border    22
  bottom_border 22
  border_fuzziness 80
  clumping_factor 50
}

create_land {
  terrain_type LEAVES
  land_percent 100
  left_border   38
  right_border  38
  top_border    38
  bottom_border 38
  border_fuzziness 80
  clumping_factor 80
  land_position 50 50
}

<ELEVATION_GENERATION>  /* hills */
/* arabia elevation settings */
if TINY_MAP
  create_elevation 4 {
    base_terrain GRASS /* change this to match base_terrain of map */
    number_of_clumps 10
    number_of_tiles 2000
    set_scale_by_groups
    set_scale_by_size
  }
else
  create_elevation 7 {
    base_terrain GRASS /* change this to match base_terrain of map */
    number_of_clumps 15
    number_of_tiles 3000
    set_scale_by_groups
    set_scale_by_size
  }
endif

<TERRAIN_GENERATION>    /* terrain patches */
/* replace cliff grass with desired terrain */
/* arabia forest settings */
/* FOREST / BAMBOO / JUNGLE
   PINE_FORST / PALM_DESERT
   SNOW_FOREST / DLC_DRAGONFOREST
   DLC_BAOBAFOREST / DLC_ACACIAFOREST
   DLC_MANGROVEFOREST */
/*
create_terrain FOREST {
  base_terrain GRASS /* change to base terrain of the map */
  land_percent 12
  number_of_clumps 10
  set_scale_by_groups
  set_avoid_player_start_areas
}
*/
create_terrain WATER {
  base_terrain LEAVES
  land_percent 100
  number_of_clumps 9999
  spacing_to_other_terrain_types 2
}
create_terrain MED_WATER {
  base_terrain WATER
  land_percent 100
  number_of_clumps 9999
  spacing_to_other_terrain_types 2
}
create_terrain DLC_WATER4 {
  base_terrain MED_WATER
  land_percent 100
  number_of_clumps 9999
  spacing_to_other_terrain_types 2
}

<CONNECTION_GENERATION> /* bridges/roads */
create_connect_all_players_land {
  replace_terrain SNOW_FOREST SNOW
  terrain_cost SNOW 7
  terrain_cost SNOW_FOREST 2
  terrain_size SNOW_FOREST 7
  terrain_size SNOW 0
}

<OBJECTS_GENERATION>    /* units, buildings, resources, eyecandy */

create_object STONE {
  terrain_to_place_on LEAVES
  number_of_objects 99999
  temp_min_distance_group_placement 1
}
create_object GOLD {
  terrain_to_place_on LEAVES
  number_of_objects 99999
}

/* ---Begin standard resources--- */

/* standard start */
create_object TOWN_CENTER {
  set_place_for_every_player
  group_placement_radius 18
  min_distance_to_players 0
  max_distance_to_players 0
}
create_object VILLAGER {
  set_place_for_every_player
  min_distance_to_players 6
  max_distance_to_players 6
  min_distance_group_placement 1
}
create_object SCOUT {
  set_place_for_every_player
  min_distance_to_players 7
  max_distance_to_players 9
  min_distance_group_placement 1
}

/* 3 relics per map, 1 relic per player. ludikris map gets ludikris relics */
if LUDIKRIS_MAP
  create_object RELIC {
    number_of_objects            250
    min_distance_to_players     25
    temp_min_distance_group_placement 110
    max_distance_to_other_zones 4
  }
else
  create_object RELIC {
    number_of_objects            3
    min_distance_to_players     40
    temp_min_distance_group_placement 20
    max_distance_to_other_zones 4
  }
  create_object RELIC {
    number_of_objects	1
    number_of_groups 1
    set_gaia_object_only
    set_place_for_every_player
    min_distance_to_players 20
    max_distance_to_players 26
    temp_min_distance_group_placement 20
    max_distance_to_other_zones 4
  }
endif

/* regicide support */
if REGICIDE
  create_object KING {
    set_place_for_every_player
    min_distance_to_players 6
    max_distance_to_players 6
    min_distance_group_placement 1
  }
  create_object VILLAGER {
    number_of_objects 7
    set_place_for_every_player
    min_distance_to_players 6
    max_distance_to_players 6
    min_distance_group_placement 1
  }
  create_object CASTLE {
    set_place_for_every_player
    min_distance_to_players 10
    max_distance_to_players 10
  }
endif

/* berries */
/* FORAGE / DLC_ORANGEBUSH */
create_object FORAGE {
  number_of_objects 3
  group_placement_radius 3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 10
  max_distance_to_players 12
  min_distance_group_placement 6
}

/* sheepies */
/* SHEEP / TURKEY / DLC_GOAT
   DLC_LLAMA / DLC_WATERBUFFALO
   DLC_COW */
create_object DLC_COW {
  number_of_objects 4
  number_of_groups 1
  temp_min_distance_group_placement 2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 6
  max_distance_to_players 6
  min_distance_group_placement 1
  temp_min_distance_group_placement 4
}
create_object DLC_COW {
  number_of_objects 14
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players 12
  min_distance_group_placement 1
  temp_min_distance_group_placement 6
  terrain_to_place_on SNOW
}

/* stragglers */
/* OAKTREE / PINETREE / SNOWPINETREE
   PALMTREE / JUNGLETREE / DLC_RAINTREE
   DLC_DRAGONTREE / DLC_BAOBABTREE 
   DEADTREE / DEADTREE2 / DLC_MANGROVE_TREE 
   BAMBOO_TREE */
create_object DEADTREE2 {
  number_of_objects 2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 4
  max_distance_to_players 5
  min_distance_group_placement 2
}
create_object DEADTREE {
  number_of_objects 3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 6
  max_distance_to_players 8
  min_distance_group_placement 2
}

/* predatory lifeforms */
/* WOLF / JAGUAR / DLC_KOMODO / DLC_TIGER 
   DLC_LION / DLC_CROCODILE / DLC_BEAR */
create_object DLC_BEAR {
  number_of_objects 1
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players 20
  min_distance_group_placement 1
  temp_min_distance_group_placement 6
  terrain_to_place_on SNOW
}
create_object WOLF {
  number_of_objects 2
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players 20
  min_distance_group_placement 1
  temp_min_distance_group_placement 6
  terrain_to_place_on SNOW
}

/* birds */
/* HAWK / MACAW / DLC_STORK / DLC_VULTURE / DLC_FALCON */
create_object DLC_FALCON {
  number_of_objects 6
  set_scaling_to_map_size
}

create_object FISH {
  temp_min_distance_group_placement 3
  max_distance_to_other_zones 2
  number_of_objects 99999
  min_distance_group_placement 2
}
