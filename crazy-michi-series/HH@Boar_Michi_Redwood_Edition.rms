<PLAYER_SETUP>          /* basic setup */

<LAND_GENERATION>       /* main lands and seas */
base_terrain DLC_JUNGLEGRASS

create_player_lands {
  terrain_type GRASS
  land_percent 100
  other_zone_avoidance_distance 15
  base_size 10
  set_zone_by_team
  border_fuzziness 100
  clumping_factor 100
}

<ELEVATION_GENERATION>  /* hills */
create_elevation 2 {
  base_terrain GRASS
  number_of_tiles 200
  number_of_clumps 7
  set_scale_by_size
  set_scale_by_groups
  spacing 2
}

<TERRAIN_GENERATION>    /* terrain patches */
/* forest */
create_terrain FOREST {
  base_terrain GRASS
  land_percent 10
  number_of_clumps 7
  spacing_to_other_terrain_types 5
  set_avoid_player_start_areas
  set_scale_by_groups
}
create_terrain PINE_FOREST {
  base_terrain GRASS
  land_percent 10
  number_of_clumps 7
  spacing_to_other_terrain_types 5
  set_avoid_player_start_areas
  set_scale_by_groups
}

create_terrain GRASS3
{
	base_terrain					GRASS
	spacing_to_other_terrain_types	0
	land_percent					3
	number_of_clumps				40
	clumping_factor					1
	set_avoid_player_start_areas
	set_scale_by_groups
}

create_terrain GRASS2
{
	base_terrain					GRASS
	spacing_to_other_terrain_types	0
	land_percent					7
	number_of_clumps				40
	set_avoid_player_start_areas
	set_scale_by_groups
}

create_terrain DIRT3
{
	base_terrain					GRASS
	spacing_to_other_terrain_types	0
	land_percent					1
	number_of_clumps				15
	set_avoid_player_start_areas
  set_scale_by_groups
}

<CONNECTION_GENERATION> /* bridges/roads */

<OBJECTS_GENERATION>    /* units, buildings, resources, eyecandy */

/* normal start */
create_object TOWN_CENTER {
  set_place_for_every_player
  max_distance_to_players 0
  min_distance_to_players 0
}
create_object SCOUT {
  set_place_for_every_player
  min_distance_to_players 4
  max_distance_to_players 6
}
create_object VILLAGER {
  set_place_for_every_player
  min_distance_to_players 4
  max_distance_to_players 6
}

/* if regicide... */
if REGICIDE
  create_object KING {
    set_place_for_every_player
    min_distance_to_players 4
    max_distance_to_players 6
  }
  create_object CASTLE {
    set_place_for_every_player
    min_distance_to_players 10
    max_distance_to_players 20
  }
  create_object VILLAGER {
    set_place_for_every_player
    min_distance_to_players 4
    max_distance_to_players 6
    number_of_objects 7
  }
endif
/* few iron boars in the middle */
create_object IRON_BOAR {
  number_of_objects 9999
  set_scaling_to_map_size
  terrain_to_place_on DLC_JUNGLEGRASS
	temp_min_distance_group_placement 5
}

/* boars in the middle */
create_object BOAR {
  number_of_objects 9999
  set_scaling_to_map_size
  terrain_to_place_on DLC_JUNGLEGRASS
}

/* fill the gaps on the edges with rocks */
create_object ROCK {
  number_of_objects 100
  terrain_to_place_on DLC_JUNGLEGRASS
}

/* wolves */
create_object WOLF {
  number_of_groups 5
  number_of_objects 1
  set_scaling_to_map_size
  temp_min_distance_group_placement 20
  min_distance_to_players 32
}

/* per-player resources */
create_object SHEEP {
  set_place_for_every_player
  set_gaia_object_only
  number_of_objects 4
  number_of_groups 2
  set_loose_grouping
  min_distance_to_players 4
  max_distance_to_players 6
}
create_object BOAR {
  set_place_for_every_player
  set_gaia_object_only
  number_of_objects 2
  number_of_groups 1
	min_distance_to_players			12
	max_distance_to_players			14
}
create_object DEER {
  number_of_objects 4
  number_of_groups 1
  group_placement_radius 3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 10
  max_distance_to_players 12
  max_distance_to_other_zones 4
}
create_object OAKTREE {
  set_place_for_every_player
  set_gaia_object_only
  number_of_objects 3
  min_distance_group_placement 2
  min_distance_to_players 5
  max_distance_to_players 5
}
create_object FORAGE {
	number_of_objects				6
	group_placement_radius			3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players			10
	max_distance_to_players			12
	min_distance_group_placement	6
}

create_object GOLD {
  number_of_objects          7
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    10
  max_distance_to_players    15
  min_distance_group_placement  3
  max_distance_to_other_zones 4
}

create_object GOLD {
  number_of_objects          3
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    12
  max_distance_to_players    17
  min_distance_group_placement  3
  max_distance_to_other_zones 4
}

create_object GOLD {
	number_of_objects				4
	group_placement_radius			3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players			25
	max_distance_to_players			35
	min_distance_group_placement	3
  max_distance_to_other_zones 4
}

create_object STONE {
  number_of_objects          5
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    10
  max_distance_to_players    15
  min_distance_group_placement  3
  max_distance_to_other_zones 4
}

create_object STONE {
  number_of_objects          3
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    12
  max_distance_to_players    17
  min_distance_group_placement  3
  max_distance_to_other_zones 4
}

create_object STONE {
	number_of_objects				4
	group_placement_radius			3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players			25
	max_distance_to_players			35
	min_distance_group_placement	3
  max_distance_to_other_zones 4
}

/* EXTRA GOLD */
create_object GOLD
{
  number_of_groups		4
  number_of_objects		5
  group_variance			1
	group_placement_radius			3
	set_tight_grouping
	set_gaia_object_only
	min_distance_to_players			30
	min_distance_group_placement	5
  max_distance_to_other_zones 10
  set_scaling_to_map_size
}

/* EXTRA STONE */
create_object STONE
{
  number_of_groups		4
  number_of_objects		5
  group_variance			1
	group_placement_radius			3
	set_tight_grouping
	set_gaia_object_only
	min_distance_to_players			30
	min_distance_group_placement	5
  max_distance_to_other_zones 10
  set_scaling_to_map_size
}

/* relics and goodies */
create_object RELIC {
  number_of_objects	1
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 20
  max_distance_to_players 26
  temp_min_distance_group_placement 20
  max_distance_to_other_zones 4
  terrain_to_place_on GRASS
}
/* extra relics */
create_object RELIC {
  number_of_objects	1
  number_of_groups	3
  set_scaling_to_map_size
  set_gaia_object_only
  min_distance_to_players 30
  temp_min_distance_group_placement 20
  max_distance_to_other_zones 4
  terrain_to_place_on GRASS
}

create_object HAWK {
  number_of_objects 6
  set_scaling_to_map_size
}
