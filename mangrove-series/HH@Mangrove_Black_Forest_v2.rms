/* **************************************** */
/* Map:     Mangrove Black Forest           */
/* Version: 2.0                             */
/* Author:  HousedHorse                     */
/* Date:    08-12-2018                      */
/* **************************************** */

/* some const definitions */
#const DLC_FALCON   1056
#const DEADTREE     404
#const DEADTREE2    407
#const GRASS_CLIFFS 16

<PLAYER_SETUP>          /* basic setup */
random_placement

<LAND_GENERATION>       /* main lands and seas */
base_terrain DLC_MANGROVEFOREST

create_player_lands {
  terrain_type DLC_MANGROVESHALLOW
  land_percent 34
  base_size 11
  other_zone_avoidance_distance 20
}

<ELEVATION_GENERATION>  /* hills */

<TERRAIN_GENERATION>    /* terrain patches */
create_terrain DLC_BEACH2 {
  base_terrain DLC_MANGROVESHALLOW
  land_percent 4
  number_of_clumps 8
  set_scale_by_groups
  set_avoid_player_start_areas
}

<CONNECTION_GENERATION> /* bridges/roads */
create_connect_all_players_land {
  replace_terrain DLC_MANGROVESHALLOW DLC_MANGROVESHALLOW
  replace_terrain DLC_MANGROVEFOREST DLC_MANGROVESHALLOW
  replace_terrain DLC_BEACH2 DLC_MANGROVESHALLOW
  terrain_cost DLC_MANGROVEFOREST 7
  terrain_cost DLC_BEACH2 7
  terrain_cost DLC_MANGROVESHALLOW 1
}

create_connect_teams_lands {
  replace_terrain DLC_MANGROVESHALLOW DLC_BEACH2
  replace_terrain DLC_MANGROVEFOREST DLC_BEACH2
  replace_terrain DLC_BEACH2 DLC_BEACH2
  terrain_cost DLC_MANGROVEFOREST 7
  terrain_cost DLC_MANGROVESHALLOW 2
  terrain_cost DLC_BEACH2 1
}

<OBJECTS_GENERATION>    /* units, buildings, resources, eyecandy */

/* ---Begin standard resources--- */

/* standard start */
create_object TOWN_CENTER {
  set_place_for_every_player
  group_placement_radius 18
  min_distance_to_players 0
  max_distance_to_players 0
}
create_object VILLAGER {
  set_place_for_every_player
  min_distance_to_players 6
  max_distance_to_players 6
}
create_object SCOUT {
  set_place_for_every_player
  min_distance_to_players 7
  max_distance_to_players 9
}

/* 3 relics per map, 1 relic per player. ludikris map gets ludikris relics */
if LUDIKRIS_MAP
  create_object RELIC {
    number_of_objects            250
    min_distance_to_players     25
    temp_min_distance_group_placement 110
    max_distance_to_other_zones 4
    terrain_to_place_on DLC_MANGROVESHALLOW
  }
else
  create_object RELIC {
    number_of_objects            3
    min_distance_to_players     40
    temp_min_distance_group_placement 20
    max_distance_to_other_zones 4
    terrain_to_place_on DLC_MANGROVESHALLOW
  }
  create_object RELIC {
    number_of_objects	1
    number_of_groups 1
    set_gaia_object_only
    set_place_for_every_player
    min_distance_to_players 20
    max_distance_to_players 26
    temp_min_distance_group_placement 20
    max_distance_to_other_zones 4
    terrain_to_place_on DLC_MANGROVESHALLOW
  }
endif

/* regicide support */
if REGICIDE
  create_object KING {
    set_place_for_every_player
    min_distance_to_players 4
    max_distance_to_players 6
  }
  create_object VILLAGER {
    number_of_objects 7
    set_place_for_every_player
    min_distance_to_players 6
    max_distance_to_players 6
  }
  create_object CASTLE {
    set_place_for_every_player
    min_distance_to_players 10
    max_distance_to_players 20
    terrain_to_place_on DLC_MANGROVESHALLOW
  }
endif

/* berries */
/* FORAGE / DLC_ORANGEBUSH */
create_object DLC_ORANGEBUSH {
  number_of_objects 6
  group_placement_radius 3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 10
  max_distance_to_players 12
  min_distance_group_placement 6
  terrain_to_place_on DLC_MANGROVESHALLOW
}

/* near gold */
create_object GOLD {
  number_of_objects 7
  group_placement_radius 3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 12
  max_distance_to_players 16
  min_distance_group_placement 3
  max_distance_to_other_zones 7
  terrain_to_place_on DLC_MANGROVESHALLOW
}

/* medium gold */
create_object GOLD {
  number_of_objects 4
  group_placement_radius 3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 14
  max_distance_to_players 18
  min_distance_group_placement 3
  max_distance_to_other_zones 7
  terrain_to_place_on DLC_MANGROVESHALLOW
}

/* far gold */
create_object GOLD {
  number_of_objects 4
  group_placement_radius 3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 14
  max_distance_to_players 18
  min_distance_group_placement 3
  max_distance_to_other_zones 7
  terrain_to_place_on DLC_MANGROVESHALLOW
}

/* near stone */
create_object STONE {
  number_of_objects 5
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 14
  max_distance_to_players 18
  min_distance_group_placement 3
  max_distance_to_other_zones 7
  terrain_to_place_on DLC_MANGROVESHALLOW
}

/* far stone */
create_object STONE {
  number_of_objects 4
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 14
  max_distance_to_players 18
  min_distance_group_placement 3
  max_distance_to_other_zones 7
  terrain_to_place_on DLC_MANGROVESHALLOW
}

/* sheepies */
/* SHEEP / TURKEY / DLC_GOAT
   DLC_LLAMA / DLC_WATERBUFFALO
   DLC_COW */
create_object DLC_WATERBUFFALO {
   number_of_objects 4
   number_of_groups 2
   temp_min_distance_group_placement 2
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players 6
   max_distance_to_players 6
  min_distance_group_placement 1
}

/* boars */
/* BOAR / JAVELINA / DLC_ELEPHANT
   DLC_RHINO */
create_object DLC_RHINO {
  number_of_objects 2
  number_of_groups 3
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 14
  max_distance_to_players 16
  min_distance_group_placement 3
  terrain_to_place_on DLC_MANGROVESHALLOW
}

/* stragglers */
/* OAKTREE / PINETREE / SNOWPINETREE
   PALMTREE / JUNGLETREE / DLC_RAINTREE
   DLC_DRAGONTREE / DLC_BAOBABTREE 
   DEADTREE / DEADTREE2 / DLC_MANGROVE_TREE */
create_object DLC_MANGROVE_TREE {
  number_of_objects 2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 4
  max_distance_to_players 5
  min_distance_group_placement 2
}
create_object DLC_MANGROVE_TREE {
  number_of_objects 3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 6
  max_distance_to_players 8
  min_distance_group_placement 2
}

/* ---End of standard resources--- */

/* ---Begin additional resources by map size--- */

create_object GOLD {
  if TINY_MAP
    number_of_groups 2
    number_of_objects 3
    min_distance_group_placement 9
  elseif SMALL_MAP
    number_of_groups 2
    number_of_objects 3
    min_distance_group_placement 9
  elseif MEDIUM_MAP
    number_of_groups 3
    number_of_objects 3
    group_variance 1
    min_distance_group_placement 9
  elseif LARGE_MAP
    number_of_groups 3
    number_of_objects 3
    group_variance 1
    min_distance_group_placement 9
  elseif HUGE_MAP
    number_of_groups 4
    number_of_objects 4
    group_variance 1
    min_distance_group_placement 9
  elseif GIGANTIC_MAP
    number_of_groups 5
    number_of_objects 4
    group_variance 1
    min_distance_group_placement 9
  elseif LUDIKRIS_MAP
    number_of_groups 250
    number_of_objects 4
    group_variance 1
    temp_min_distance_group_placement  44
  endif 
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players 40
  terrain_to_place_on DLC_MANGROVESHALLOW
}

create_object STONE {
  if TINY_MAP
    number_of_groups 1
    min_distance_group_placement 9
  elseif SMALL_MAP
    number_of_groups 1
    min_distance_group_placement 9
  elseif MEDIUM_MAP
    number_of_groups 2
    min_distance_group_placement 9
  elseif LARGE_MAP
    number_of_groups 3
    min_distance_group_placement 9
  elseif HUGE_MAP
    number_of_groups 4
    min_distance_group_placement 9
  elseif GIGANTIC_MAP
    number_of_groups 5
    min_distance_group_placement 9
  elseif LUDIKRIS_MAP
    number_of_groups 250
    temp_min_distance_group_placement 44
  endif 
  number_of_objects 4
  group_variance 1
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players 40
  terrain_to_place_on DLC_MANGROVESHALLOW
}

/* ---End of additional resources by map size--- */

/* birds */
/* HAWK / MACAW / DLC_STORK / DLC_VULTURE / DLC_FALCON */
create_object HAWK {
  number_of_objects 6
  set_scaling_to_map_size
}
