/* **************************************** */
/* Map:     Wetland                         */
/* Version: 1.0                             */
/* Author:  HousedHorse                     */
/* Date:    08/06/2018                      */
/* **************************************** */

/* some const definitions */
#const DLC_FALCON   1056
#const DEADTREE     404
#const DEADTREE2    407
#const GRASS_CLIFFS 16

<PLAYER_SETUP>          /* basic setup */
random_placement

<LAND_GENERATION>       /* main lands and seas */
base_terrain DLC_JUNGLEGRASS

create_player_lands {
  terrain_type DLC_JUNGLEGRASS
  land_percent 10
  base_size 10
  left_border   -2
  right_border  -2
  top_border    -2
  bottom_border -2
  border_fuzziness 20
  clumping_factor 20
}

create_land {
  terrain_type DLC_MANGROVESHALLOW
  left_border   20
  right_border  20
  top_border    20
  bottom_border 20
  border_fuzziness 20
  clumping_factor 20
}

<ELEVATION_GENERATION>  /* hills */
if TINY_MAP
  create_elevation 2 {
    base_terrain DLC_JUNGLEGRASS
    number_of_clumps 3
    number_of_tiles 500
    set_scale_by_groups
    set_scale_by_size
  }
else
  create_elevation 4 {
    base_terrain DLC_JUNGLEGRASS
    number_of_clumps 5
    number_of_tiles 1000
    set_scale_by_groups
    set_scale_by_size
  }
endif

<TERRAIN_GENERATION>    /* terrain patches */
/* forest in the middle */
create_terrain DLC_MANGROVEFOREST {
  base_terrain DLC_MANGROVESHALLOW
  land_percent 12
  number_of_clumps 36
  set_scale_by_groups
  set_avoid_player_start_areas
  spacing_to_other_terrain_types 2
}

/* rainforests */
create_terrain DLC_RAINFOREST {
  base_terrain DLC_JUNGLEGRASS
  land_percent 12
  number_of_clumps 11
  set_scale_by_groups
  set_avoid_player_start_areas
  spacing_to_other_terrain_types 5
}

/* grassy clumps */
create_terrain GRASS {
  base_terrain DLC_JUNGLEGRASS
  number_of_clumps 200
  land_percent 12
  set_scale_by_groups
}
create_terrain GRASS2 {
  base_terrain DLC_JUNGLEGRASS
  number_of_clumps 200
  land_percent 12
  set_scale_by_groups
}

create_terrain DLC_JUNGLEGRASS {
  base_terrain BEACH
  land_percent 100
  number_of_clumps 99999
}

/* vary swampland in the middle */
create_terrain DLC_NEWSHALLOW {
  base_terrain DLC_MANGROVESHALLOW
  land_percent 2
  number_of_clumps 9999
  set_scale_by_groups
  set_avoid_player_start_areas
  spacing_to_other_terrain_types 2
}

<CONNECTION_GENERATION> /* bridges/roads */

<OBJECTS_GENERATION>    /* units, buildings, resources, eyecandy */

/* ---Begin standard resources--- */

/* standard start */
create_object TOWN_CENTER {
  set_place_for_every_player
  group_placement_radius 18
  min_distance_to_players 0
  max_distance_to_players 0
}
create_object VILLAGER {
  set_place_for_every_player
  min_distance_to_players 6
  max_distance_to_players 6
}
create_object SCOUT {
  set_place_for_every_player
  min_distance_to_players 7
  max_distance_to_players 9
}

/* 3 relics per map, 1 relic per player. ludikris map gets ludikris relics */
if LUDIKRIS_MAP
  create_object RELIC {
    number_of_objects            250
    min_distance_to_players     25
    temp_min_distance_group_placement 110
    max_distance_to_other_zones 4
  }
else
  create_object RELIC {
    number_of_objects            3
    min_distance_to_players     40
    temp_min_distance_group_placement 20
    max_distance_to_other_zones 4
  }
  create_object RELIC {
    number_of_objects	1
    number_of_groups 1
    set_gaia_object_only
    set_place_for_every_player
    min_distance_to_players 20
    max_distance_to_players 26
    temp_min_distance_group_placement 20
    max_distance_to_other_zones 4
  }
endif

/* regicide support */
if REGICIDE
  create_object KING {
    set_place_for_every_player
    min_distance_to_players 4
    max_distance_to_players 6
  }
  create_object VILLAGER {
    number_of_objects 7
    set_place_for_every_player
    min_distance_to_players 6
    max_distance_to_players 6
  }
  create_object CASTLE {
    set_place_for_every_player
    min_distance_to_players 10
    max_distance_to_players 10
  }
endif

/* berries */
/* FORAGE / DLC_ORANGEBUSH */
create_object FORAGE {
  number_of_objects 6
  group_placement_radius 3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 10
  max_distance_to_players 12
  min_distance_group_placement 6
}

/* near gold */
create_object GOLD {
  number_of_objects 7
  group_placement_radius 3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 12
  max_distance_to_players 16
  min_distance_group_placement 3
  max_distance_to_other_zones 7
}

/* medium gold */
create_object GOLD {
  number_of_objects 4
  group_placement_radius 3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 18
  max_distance_to_players 26
  min_distance_group_placement 3
  max_distance_to_other_zones 7
}

/* far gold */
create_object GOLD {
  number_of_objects 4
  group_placement_radius 3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 25
  max_distance_to_players 35
  min_distance_group_placement 3
  max_distance_to_other_zones 7
}

/* near stone */
create_object STONE {
  number_of_objects 5
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 14
  max_distance_to_players 18
  min_distance_group_placement 3
  max_distance_to_other_zones 7
}

/* far stone */
create_object STONE {
  number_of_objects 4
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 20
  max_distance_to_players 26
  min_distance_group_placement 3
  max_distance_to_other_zones 7
}

/* sheepies */
/* SHEEP / TURKEY / DLC_GOAT
   DLC_LLAMA / DLC_WATERBUFFALO
   DLC_COW */
create_object DLC_WATERBUFFALO {
   number_of_objects 4
   number_of_groups 2
   temp_min_distance_group_placement 2
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players 6
   max_distance_to_players 6
}

/* deer patch */
/* DEER / DLC_OSTRICH / DLC_ZEBRA */
create_object DEER {
  number_of_objects 4
  group_placement_radius 3
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 14
  max_distance_to_players 16
  terrain_to_place_on DLC_JUNGLEGRASS
  min_distance_group_placement 3
}

/* boars */
/* BOAR / JAVELINA / DLC_ELEPHANT
   DLC_RHINO */
create_object DLC_RHINO {
  number_of_objects 2
  number_of_groups 1
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 14
  max_distance_to_players 16
  terrain_to_place_on DLC_JUNGLEGRASS
  min_distance_group_placement 3
}

/* stragglers */
/* OAKTREE / PINETREE / SNOWPINETREE
   PALMTREE / JUNGLETREE / DLC_RAINTREE
   DLC_DRAGONTREE / DLC_BAOBABTREE 
   DEADTREE / DEADTREE2 */
create_object DLC_RAINTREE {
  number_of_objects 2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 4
  max_distance_to_players 5
  min_distance_group_placement 2
}
create_object DLC_RAINTREE {
  number_of_objects 3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 6
  max_distance_to_players 8
  min_distance_group_placement 2
}

/* predatory lifeforms */
/* WOLF / JAGUAR / DLC_KOMODO / DLC_TIGER 
   DLC_LION / DLC_CROCODILE / DLC_BEAR */
create_object DLC_TIGER {
  number_of_objects 2
  set_gaia_object_only
  set_place_for_every_player
  temp_min_distance_group_placement 12
  min_distance_to_players 30
  max_distance_to_players 50
  terrain_to_place_on DLC_JUNGLEGRASS
}
/* additional predatory lifeforms in the center */
create_object DLC_CROCODILE {
  number_of_objects 10
  set_gaia_object_only
  temp_min_distance_group_placement 12
  min_distance_to_players 25
  terrain_to_place_on DLC_NEWSHALLOW
  set_scale_to_map_size
}
create_object DLC_CROCODILE {
  number_of_objects 10
  set_gaia_object_only
  temp_min_distance_group_placement 12
  min_distance_to_players 25
  terrain_to_place_on DLC_MANGROVESHALLOW
  set_scale_to_map_size
}

/* ---End of standard resources--- */

/* ---Begin additional resources by map size--- */

create_object GOLD {
  if TINY_MAP
    number_of_groups 2
    number_of_objects 3
    min_distance_group_placement 5
  elseif SMALL_MAP
    number_of_groups 2
    number_of_objects 3
    min_distance_group_placement 5
  elseif MEDIUM_MAP
    number_of_groups 3
    number_of_objects 3
    group_variance 1
    min_distance_group_placement 5
  elseif LARGE_MAP
    number_of_groups 3
    number_of_objects 3
    group_variance 1
    min_distance_group_placement 5
  elseif HUGE_MAP
    number_of_groups 4
    number_of_objects 4
    group_variance 1
    min_distance_group_placement 5
  elseif GIGANTIC_MAP
    number_of_groups 5
    number_of_objects 4
    group_variance 1
    min_distance_group_placement 5
  elseif LUDIKRIS_MAP
    number_of_groups 250
    number_of_objects 4
    group_variance 1
    temp_min_distance_group_placement  44
  endif 
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players 40
}

create_object STONE {
  if TINY_MAP
    number_of_groups 1
    min_distance_group_placement 5
  elseif SMALL_MAP
    number_of_groups 1
    min_distance_group_placement 5
  elseif MEDIUM_MAP
    number_of_groups 2
    min_distance_group_placement 5
  elseif LARGE_MAP
    number_of_groups 3
    min_distance_group_placement 5
  elseif HUGE_MAP
    number_of_groups 4
    min_distance_group_placement 5
  elseif GIGANTIC_MAP
    number_of_groups 5
    min_distance_group_placement 5
  elseif LUDIKRIS_MAP
    number_of_groups 250
    temp_min_distance_group_placement 44
  endif 
  number_of_objects 4
  group_variance 1
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players 40
}

/* ---End of additional resources by map size--- */

/* birds */
/* HAWK / MACAW / DLC_STORK / DLC_VULTURE / DLC_FALCON */
create_object HAWK {
  number_of_objects 6
  set_scaling_to_map_size
}
